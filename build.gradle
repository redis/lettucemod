plugins {
    id 'distribution'
    id 'java-library'
    id 'java-library-distribution'
    id 'maven-publish'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

group = 'com.redislabs'
description = 'Java client for Redis Labs modules'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    api 'io.lettuce:lettuce-core:6.0.1.RELEASE'
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.11.3'
    testImplementation 'io.projectreactor:reactor-test:3.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.testcontainers:testcontainers:1.15.0'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.0'
    testImplementation 'org.apache.commons:commons-pool2:2.9.0'
    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
}

test {
    useJUnitPlatform()
}

distributions {
    main {
        contents {
            from (project.docsDir) {
                into 'docs'
            }
            from 'README.adoc'
            from 'LICENSE'
        }
    }
}

distTar {
    compression = Compression.GZIP
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Mesclun'
                description = 'Java client for Redis Labs module'
                url = 'https://github.com/redis-developer/mesclun'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jruaux'
                        name = 'Julien Ruaux'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/redis-developer/mesclun.git'
                    developerConnection = 'scm:git:git@github.com:redis-developer/mesclun.git'
                    url = 'https://github.com/redis-developer/mesclun'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ''
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : ''
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/redis-developer/mesclun.git'
        version {
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
                password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
            }
        }
    }
}

githubRelease {
    token = project.hasProperty('githubToken') ? project.property('githubToken') : ''
    owner "redis-developer"
    repo "mesclun"
    releaseAssets distZip, distTar
    draft true
    body changelog()
}

def isNonStable = { String version ->
  def nonStableKeyword = ['PREVIEW'].any { it -> version.toUpperCase().contains(it) }
  def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE8'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+([.-]r)?$/
  return nonStableKeyword || (!stableKeyword && !(version ==~ regex))
}

tasks.named("dependencyUpdates").configure {

  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }

}

tasks.distZip.shouldRunAfter tasks.javadocJar
check.dependsOn jacocoTestReport
afterReleaseBuild.dependsOn ":githubRelease"
afterReleaseBuild.dependsOn bintrayUpload
